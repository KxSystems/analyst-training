// @fileOverview 
// The haversine function gives the distance between two 
// points on a great circle
//
// @param start {float[]} Lat-Long tuple
// @param end {float[]} Lat-Long tuple
//
// @return {float} The distance between the points
{[start; end]

    // Create variables to quickly reference the components 
    // of the two points
    slat    : start 0;
    slong   : start 1;
    elat    : end 0;
    elong   : end 1;
    
    // Fetch the radius of the current planet
    radius : PLANETS[Planet; `radius];
    
    square: {x*x};
    
    // The Haversine function
    deltaLat    : i.degToRad elat - slat;
    deltaLong   : i.degToRad elong - slong;
   
    a: square[sin deltaLat % 2] + 
       square[sin deltaLong % 2] * cos[i.degToRad slat] * cos i.degToRad elat;  
    
    c: 2 * i.atan2[sqrt a; sqrt 1 - a]; 
    
    : radius * c;
    }